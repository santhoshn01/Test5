plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'com.jfrog.artifactory' version '4.26.2'
    id 'org.sonarqube' version '6.1.0.5360'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'checkstyle'
    id 'pmd'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
def isSnapshot = version.endsWith('-SNAPSHOT')

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = '10.12.4'
    configFile = file("config/checkstyle/checkstyle.xml")
    // Optional: fail build on violations
    ignoreFailures = false
}

pmd {
    toolVersion = '6.55.0'
    ruleSets = [] // disable built-in
    ruleSetFiles = files("config/pmd/ruleset.xml")
    ignoreFailures = false
}

tasks.withType(Checkstyle).configureEach {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

tasks.withType(Pmd).configureEach {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jacocoTestReport') {
    dependsOn test
    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)
        html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
    }
}

tasks.named('shadowJar') {
    archiveBaseName.set('app')
    archiveVersion.set(version)
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': 'com.example.demo.DemoApplication'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar
        }
    }
}

artifactory {
    contextUrl = 'http://192.168.100.62:8082/artifactory'
    publish {
        repository {
            repoKey = isSnapshot ? 'libs-snapshot-local' : 'libs-release-local'
            username = project.findProperty("artifactory_user") ?: "admin"
            password = project.findProperty("artifactory_password") ?: "Chella*123"
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishPom = true
        }
    }
}
